# Copyright 2022 The Lynx Authors. All rights reserved.
# Licensed under the Apache License Version 2.0 that can be found in the
# LICENSE file in the root directory of this source tree.

import("../../../testing/test.gni")
import("../../Lynx.gni")

# worklet_shared_sources
worklet_shared_sources = [
  "../../runtime/bindings/napi/worklet/napi_frame_callback.cc",
  "../../runtime/bindings/napi/worklet/napi_frame_callback.h",
  "../../runtime/bindings/napi/worklet/napi_func_callback.cc",
  "../../runtime/bindings/napi/worklet/napi_func_callback.h",
  "../../runtime/bindings/napi/worklet/napi_lepus_component.cc",
  "../../runtime/bindings/napi/worklet/napi_lepus_component.h",
  "../../runtime/bindings/napi/worklet/napi_lepus_element.cc",
  "../../runtime/bindings/napi/worklet/napi_lepus_element.h",
  "../../runtime/bindings/napi/worklet/napi_lepus_gesture.cc",
  "../../runtime/bindings/napi/worklet/napi_lepus_gesture.h",
  "../../runtime/bindings/napi/worklet/napi_lepus_lynx.cc",
  "../../runtime/bindings/napi/worklet/napi_lepus_lynx.h",
  "../../runtime/bindings/napi/worklet/napi_loader_ui.cc",
  "../../runtime/bindings/napi/worklet/napi_loader_ui.h",
  "base/worklet_utils.cc",
  "base/worklet_utils.h",
  "lepus_component.cc",
  "lepus_component.h",
  "lepus_element.cc",
  "lepus_element.h",
  "lepus_gesture.cc",
  "lepus_gesture.h",
  "lepus_lynx.cc",
  "lepus_lynx.h",
  "lepus_raf_handler.cc",
  "lepus_raf_handler.h",
]

# worklet_shared_sources end

lynx_core_source_set("worklet") {
  public_configs = [ "../../../third_party/napi:napi_config" ]

  sources = worklet_shared_sources

  public_deps = [
    "../../../third_party/rapidjson",
    "../trace:renderer_trace",
  ]
  if (!enable_unittests) {
    public_deps += [ "../css:css_decoder" ]
  }

  deps = [ "../../../base/src:base_log_headers" ]
  if (enable_napi_binding) {
    deps += [ "../../runtime/bindings/napi:napi_binding_quickjs" ]
  }
}

lynx_core_source_set("worklet_oliver") {
  sources = worklet_shared_sources
  public_deps = []
  if (!enable_unittests) {
    public_deps += [ "../css:css_decoder" ]
  }
  deps = [ "../../../base/src:base_log_headers" ]
}

unittest_set("worklet_testset") {
  sources = [
    "test/worklet_api_unittest.cc",
    "test/worklet_event_unittest.cc",
    "test/worklet_utils_unittest.cc",
  ]
  public_deps = [
    ":worklet",
    "../:tasm",
    "../../base",
    "../../runtime",
    "../../runtime/bindings/lepus",
    "../dom:dom",
    "../dom:renderer_dom",
  ]
  public_deps += [ "../../../third_party/napi:env" ]
  if (is_ios) {
    public_deps += [ "../../runtime/bindings/napi:napi_binding_jsc" ]
  } else {
    public_deps += [ "../../runtime/bindings/napi:napi_binding_quickjs" ]
  }
  data_deps = []
}

unittest_exec("worklet_unittests_exec") {
  sources = []
  deps = [
    ":worklet",
    ":worklet_testset",
    "../:tasm",
    "../../../third_party/quickjs",
    "../../runtime/bindings/lepus",
    "../../shell/testing:mock_tasm_delegate_testset",
    "../dom:dom",
    "../dom:renderer_dom",
    "../starlight",
  ]
}
