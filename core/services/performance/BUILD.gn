# Copyright 2025 The Lynx Authors. All rights reserved.
# Licensed under the Apache License Version 2.0 that can be found in the
# LICENSE file in the root directory of this source tree.

import("//${lynx_dir}/build_overrides/services_files.gni")
import("../../Lynx.gni")

performance_shared_sources = [
  "memory_monitor/memory_monitor.cc",
  "memory_monitor/memory_monitor.h",
  "memory_monitor/memory_record.cc",
  "memory_monitor/memory_record.h",
  "performance_controller.cc",
  "performance_controller.h",
  "performance_event_sender.h",
]

if (!is_encoder_ut) {
  performance_shared_sources += [
    "performance_mediator.cc",
    "performance_mediator.h",
  ]
}

if (!is_oliver_ssr && !is_oliver_node_lynx && !enable_unittests &&
    !is_encoder_ut) {
  if (is_apple) {
    performance_shared_sources += [
      "darwin/performance_controller_darwin.h",
      "darwin/performance_controller_darwin.mm",
    ]
  } else if (is_harmony) {
    performance_shared_sources += [
      "harmony/performance_controller_harmony.cc",
      "harmony/performance_controller_harmony.h",
    ]
  } else if (is_android) {
    performance_shared_sources += [
      "android/performance_controller_android.cc",
      "android/performance_controller_android.h",
    ]
  }
}

lynx_core_source_set("performance") {
  sources = performance_shared_sources
  deps = [
    "../../../base/src:base_log_headers",
    "../../../third_party/rapidjson",
  ]
}
