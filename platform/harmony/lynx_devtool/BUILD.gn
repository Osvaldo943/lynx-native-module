# Copyright 2025 The Lynx Authors. All rights reserved.
# Licensed under the Apache License Version 2.0 that can be found in the
# LICENSE file in the root directory of this source tree.

import("../../../config.gni")
import("../../../core/Lynx.gni")
import("../../../devtool/lynx_devtool/devtool.gni")
import("../harmony.gni")

source_set("native") {
  public_deps = [ "src/main/cpp" ]
}

devtool_source_set("lynx_devtool_lib") {
  sources = [
    "../../../core/renderer/css/css_color.cc",
    "../../../core/renderer/css/css_color.h",
    "../../../core/renderer/css/css_keywords.cc",
    "../../../core/renderer/css/css_keywords.h",
  ]

  include_dirs = [ "../../../third_party/debug_router/src/" ]

  deps = [
    ":native",
    "../../../core/build:build",
    "../../../devtool/embedder/core:lynx_devtool_embedder_core",
    "../../../devtool/lynx_devtool:lynx_devtool_group",
    "../../../third_party/debug_router/src/debug_router/Common:debug_router_source",
  ]

  deps += [
    "../../../base/src:base",

    # devtool doesn't contain implementation of log, only deps on base_log_headers here
    "../../../base/src:base_log_headers",
    "../../../devtool/base_devtool/common:base_devtool_common_sources",
    "../../../devtool/lynx_devtool/js_debug",
    "../../../devtool/lynx_devtool/tracing",
    "../../../third_party/jsoncpp:jsoncpp",
    "../../../third_party/modp_b64:modp_b64",
  ]
}

if (enable_harmony_shared) {
  lynx_devtool_type = "shared_library"
} else {
  lynx_devtool_type = "source_set"
}

target(lynx_devtool_type, "lynx_devtool") {
  output_name = "lynxdevtool"

  deps = [
    ":lynx_devtool_lib",
    "../lynx_harmony",
  ]

  lib_dirs = [ primjs_native_lib_dir ]
  libs = [
    "ace_napi.z",
    "quick",
    "uv",
    "native_vsync",
    "ohpreferences",
  ]
}
